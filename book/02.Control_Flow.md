# Control flow
After learning about variables it is now time to learn about how to perform some logic with them.

## Comparison
ijo has the same comparison operators that you would find in other programming languages:
- a < b: a is less than b?
- a > b: a is greater than b?
- a <= b: a is less or equal to b?
- a >= b: a is greater or equal to b?
- a == b: a is equal to b?
- a != b: a is different than b?
- (a == b) && (a == c): a is equal to b? and a is equal to c?
- (a == b) || (a == c): a is equal to b? or a is equal to c?

## Groupings
Take the following expression: `1 + 2 * 3 / 2`. By default ijo will interpret it as `1 + ((2 * 3) / 2)` because multiplication and division operations take precedence over subtraction and addition. So the result would be `4`. Now if you want to change the result you can change the order of operation by grouping the operations using parenthesis: `(1 + 2 * 3) / 2` which would now give you the result `3.5`.

The `()` are used to group expressions together and produce a value, but they are used for only one expression.

Look at the following example for a better understanding:
```ijo
$number = (30 + 12);
```

In the example above, `$number` will be equal to `42` because `(30 + 12)` is a group expression that return the value of expression inside of the `()` symbols.

For the rest of this book we'll sometime refer of the syntax `(expression)` as a grouping expression, grouped expression or something similar.

## Code blocks
Code blocks are what group multiple statements together and represented by code living inside `{}` symbols. They do not produce any value, or rather they will produce the value `:nil` if not stated otherwise.

Take this example:
```ijo
$expression = {
    $name = "Jonathan";
    $fruit = :strawberry;
    $otherNumber = (40 * 10 + 20);
    writeLine("My name is \(name) and I like \(fruit)");
    writeLine("Lucky numbers: \(otherNumber), \(number)");
}
```

{{ note }}
You'll see code like this: `"My name is \(name) and I like \(fruit)"`, this uses what is called string interpolation.
You'll learn more about it in the chapter for the string type.
{{ /note }}

`$expression` will be assigned the value `:nil` because `{}` does not return a value. The reason is that, the code block contains 3 variables declaration and two function calls. The function `write` does not return any value either and we can't assume you actually want to return the value for one of the variables. Thus it was decided, that by default a block expression will return `:nil`.

You'll see that code blocks are wildly used in ijo as they are used as:
- If statements branches.
- Switch statement branches.
- Loop bodies.
- Function bodies.

Throughout this book we might refer to code inside `{}` as code blocks or scopes. Scopes will be better explain in the function chapter.

## If statements
Just like any other C-style programming language, ijo allows one to check if a statement is true or false and depending on the result
execute the code that is inside the if statement's code block.

Here is an example:
```ijo
// 1. Evaluates to :true
?(2 + 2 == 4) {
    writeLine("Yes, it's true");
}

// 2. Evaluates to :false
?(1 == 3) {
    writeLine("There's a bug in the Matrix");
}
```

To deconstruct the syntax: `?` asks a question. It wants to know if what is between the `()` is `:true` or `:false`. If it is true, then it executes the code between the code block `{ writeLine("Yes, it's true") }` in case of the first example.

In other programming languages you will also see the following type of code:
```c++
if (number == 18) {
    writeLine("18");
} else if (number == 42) {
    writeLine("42");
} else {
    writeLine("Unknown number");
}
```

Here if `number` is equal to `18` it will perform the call to `writeLine("18")` otherwise if (`else if`) `number` is equal to `42` then it will call `writeLine("42")` otherwise, if none of the conditions are met it will call `writeLine("Unknown number")`.

In languages that provide `if/else if/else` or derivative, `else if` is used to tell that the compiler should check another condition. ijo being keywordless you might imagine that other symbols are used to represent `else if`, but that is not the case. The reason is that `else if` is just another question that we ask, so we'll just reuse the same `?(expression)` syntax. To represent `else` which basically ask the question: *did we explore all the possibilites?*, we also reuse the same syntax, but this time without any expression to verify.

So the previous example, written in ijo looks like this:
```ijo
?(number == 18) {
    writeLine("18");
} ?(number == 42) {
    writeLine("42");
} ?() {
    writeLine("Unknown number");
}
```
To help read it, you can think of the code saying this to you:

Is `number` equal to `18`?
    Yes: `writeLine("18");`
    No: Is `number` equal to `42`?
        Yes: `writeLine("42");`
        No: Did we ask all of our questions?
            Yes: `writeLine("Unknown number");`
            No: continue executing code as normal.

## Switch statements
Remember the enum type in the previous chapter? No, then let me refresh your memory by defining one that we'll use in this section:
```ijo
$Directions = :north | :northwest | :northeast | :south | :southwest | :southeast | :east | :west;
$direction = :north;
```

If you want to execute specific code depending on a specific direction, you might end-up writting something like this:
```ijo
?(direction == :north) {
    writeLine("north");
} ?(direction == :northeast) {
    writeLine("northeast");
} ?(direction == :northwest) {
    writeLine("northwest");
} ?(direction == :south) {
    writeLine("south");
} ?(direction == :southwest) {
    writeLine("southwest");
} ?(direction == :southeast) {
    writeLine("southeast");
} ?(direction == :east) {
    writeLine("east");
} ?(direction == :west) {
    writeLine("west");
}
```

I think we can all agree that it is quite long and tedious to write, plus it repeat a lot of time very similar code. Switch statements are meant to help with that. They are like super charged if/else if/else statements. Can you guess what would be the syntax? To help you a switch is asking a question and they are related to enums.

To have the answer look at the following code that do the exact same thing as the example above, but written in switch form:
```ijo
?|direction| {
    :north { writeLine("northeast"); }
    :northeast { writeLine("northeast"); }
    :northwest { writeLine("northeast"); }
    :south { writeLine("northeast"); }
    :southeast { writeLine("northeast"); }
    :southwest { writeLine("northeast"); }
    :east { writeLine("northeast"); }
    :west { writeLine("northeast"); }
}
```

I don't know about you, but I find this easier to read. When compared to other languages, ijo's switch syntax is slightly different. So let's desconstruct it and explain each bit more in detail:

`?|direction|` shows that we want to start a switch statement, because a switch statement is basically asking the following question: *What is the value of X*, X being `direction` in our case we reuse the `?` symbol to be consistent with other part of the language where we ask questions and we follow it with the `|` symbol to express that it's a switch.

The switch statement is then followed by a code block that will contains its different cases.

As the main difference between an if statement and an switch statement is on the purpose of the question:
- If: is the expression true?
- Switch: is it true that the expression is equal to the value X?

So a switch case, is the specific value we want to check and the code to execute in case the expression we are checking is the same value.

The syntax is: `valueToBeChecked { codeToExecute; }`
In other C-like languages, the usual switch syntax is close to this:
```js
switch (value) {
    case 1:
        // Do something
        break;
    case 2:
        // Do something
        break;
    case 3:
        // Do something
        break;
}
```
They rely on the `case` keyword and the `break` keyword to delimit the scope of the code to be executed for the case. In ijo, we simply reuse the code block syntax. That's what I think makes the most sense and it keeps it consistent with the other control flow of the language which all uses code blocks as their body.

It is possible to write a switch case using parenthesis for the value check, but for familiarity sake and because I find that it clutters the code too much they can be omitted. So both of these are valid:
```ijo
?|direction| {
    (:north) {
        // Do something
    }
    :south {
        // Do something
    }
}
```

Each case of an each statement are mutually exclusive. Thanks to that, the following codes reach the same result:
```ijo
?(direction == :north || direction == :northeast || direction == :northwest) {
    writeLine("Going, north");
} ?() {
    writeLine("I don't know where I'm going);
}

?|direction| {
    :north, :northeast, :northwest {
        writeLine("Going, north");
    }
    _ {
        writeLine("I don't know where I'm going");
    }
}
```
Now you should see why `|` has been chosen to differentiate between if and switch statements. Between the enums that have mutually exclusive values defined with `|` and `||` meaning `or` it made sense to reuse this symbol in this context.

You might be like: "Hey wait a minute!, What is that '_' doing here? What does it mean? What does it mean ??!!". Well an enum is supposed to check for every possible possibilities, but it is not realistic to write a switch statement which conform to that. Would you write an infinite number of case when checking a number for example? So we need to have the equivalent of the `else` case of the if statement. In other languages they use the keyword `default` for that purpose.

We could follow the same principle and have it like this:
```ijo
?|direction| {
    :north { //Do something }
    ?|| { // Do something }
}
```
But this feels weird. Also, the `_` symbol is used in certain context to show that we omit the value. That we do not care about it. If the code reach that switch case, it means we don't know what value the checked expression has, so we don't care what it is. Well we care, but we "don't care" if you know what I mean. So we use the `_` symbol.

You might ask why we didn't use it as well for the else statement, well the answer is that it feels weird having to write the following:
```ijo
?(condition == true) {
    // true
} _ {
    // false
}

// or

?(condition)
{
    // true
}
_
{
    // false
}

// or again
?(condition) {
    // true
}
_ {
    // false
}
```

I find that this is more readable and understandable for every styles:
```ijo
?(condition) {
    // :true
} ?() {
    // :false
}

?(condition)
{
    // :true
}
?()
{
    // :false
}

?(condition) {
    // :true
}
?() {
    // :false
}
```

## Loops
The last control flow construct is the loop. It allows executing the same code 1 time, 2 time, 50 times or an infinite number of time without having to write it that same number of time. It is your run of the mill `while` loop in other languages:
```js
var i = 0;
while(i < 100) {
    console.log("i: " + i);
    i = i + 1;
}
```
The same code in ijo is written like this:
```ijo
$i = 0;
~(i < 100) {
    writeLine("i: \(i)");
    i = i + 1;
}
```
What this code does: 
- Declares a variable `i` that is our counter.
- As long as `i` is less than `100` repeat executing the code in the code block which does the following:
    - write `"i: "` and append the value of `i`
    - increment the value of `i` by 1
The loop is exited when `i` is equal to `100`. So you'll see the following printed in your screen:
```
i: 0
i: 1
i: 2
...
i: 98
i: 99
```

Here's your syntax breakdown section: `~()` is the combination of symbol that has been chosen to represent a loop. The expression in between the `()` will be executed at each iteration and as long as it evaluates to `:true` the code inside the code block will also be executed.

## Next steps
With all that acquired knowledge, you should now be able to read the code from the introduction and understand everything that it will do.

Next: [Functions](03.Functions.md)